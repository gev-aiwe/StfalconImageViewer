apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 33
        versionCode 2
        versionName "1.0.1"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

afterEvaluate {
    publishing {
        repositories {
            mavenLocal()
        }
        publications {
            release(MavenPublication) {
                from components.release
                groupId "$environment.group_id"
                version android.defaultConfig.versionName
                artifactId project.name
            }
        }
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    implementation "androidx.transition:transition:$versions.transition"
    implementation "com.github.chrisbanes:PhotoView:$versions.photoView"
    implementation "androidx.appcompat:appcompat:$versions.android_appcompat"
    implementation "androidx.recyclerview:recyclerview:$versions.android_recyclerview"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"
}

repositories {
    maven { url "https://jitpack.io" }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked"
    options.deprecation = true
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}